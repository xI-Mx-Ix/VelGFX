plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    runs {
        server {
            runDir "run-server"
        }
        client {
            runDir "run-client"
            programArgs '--username', 'dev'
        }
    }

    accessWidenerPath = project(":common").loom.accessWidenerPath
}


configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:$mixinextras_version")))

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "curse.maven:sodium-394468:6382649"
    modImplementation "curse.maven:modmenu-308702:5810603"
    modImplementation "curse.maven:lithium-360438:7246287"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    api("org.lwjgl:lwjgl-assimp:$lwjgl_version") {
        exclude group: "org.lwjgl", module: "lwjgl"
    }

    api("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-windows")
    api("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-macos")
    api("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-macos-arm64")
    api("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-linux")
    api("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-linux-arm64")

    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version") {
        exclude group: "org.lwjgl", module: "lwjgl"
    }

    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-windows")
    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-macos")
    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-macos-arm64")
    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-linux")
    shadowBundle("org.lwjgl:lwjgl-assimp:$lwjgl_version:natives-linux-arm64")
}

processResources {
    def propertiesToExpand = [
            minecraft_version: rootProject.minecraft_version,
            mod_id: rootProject.mod_id,
            mod_name: rootProject.mod_name,
            mod_license: rootProject.mod_license,
            mod_version: rootProject.mod_version,
            mod_authors: rootProject.mod_authors,
            mod_description: rootProject.mod_description,
            minecraft_version_range: rootProject.minecraft_version_range,
            loader_version_range: rootProject.loader_version_range,

            fabric_loader_version: rootProject.fabric_loader_version,
            fabric_api_version: rootProject.fabric_api_version,
            architectury_api_version: rootProject.architectury_api_version
    ]

    inputs.properties propertiesToExpand

    filesMatching(['fabric.mod.json', 'pack.mcmeta']) {
        expand propertiesToExpand + [project: project]
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

// Configures the sourcesJar task to include source code from the common module
// ensuring that the resulting sources jar contains the full codebase.
tasks.named("sourcesJar") {
    def commonSources = project(":common").sourceSets.main.allSource
    from commonSources
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = base.archivesName.get()
            from components.java

            // Filters out development and shadow artifacts to reduce file size
            // and ensure only the production jar is published.
            artifacts.removeAll { artifact ->
                artifact.classifier in ['dev', 'shadow', 'dev-shadow']
            }
        }
    }
}